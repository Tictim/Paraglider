plugins {
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

def generatedResources = file("src/generated")

sourceSets {
    main {
        resources {
            srcDirs generatedResources
            exclude ".cache/*"
        }
    }
    dataGen {
        java { srcDir "src/dataGen/java" }
        resources { srcDir "src/dataGen/resources" }

        compileClasspath += sourceSets.main.compileClasspath + sourceSets.main.output
        runtimeClasspath += sourceSets.main.runtimeClasspath + sourceSets.main.output
    }
}

loom {
    //accessWidenerPath = project(":common").loom.accessWidenerPath
    accessWidenerPath = file("src/main/resources/paraglider.fabric.accesswidener")

    runs {
        data {
            inherit client
            source = sourceSets.dataGen
            property("fabric-api.datagen")
            property("fabric-api.datagen.modid", "paraglider")
            property("fabric-api.datagen.output-dir", generatedResources.absolutePath)
            property("fabric-api.datagen.strict-validation")

            mods {
                paraglider {
                    sourceSet sourceSets.main
                    sourceSet sourceSets.dataGen
                }
            }
        }
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"

    modApi "fuzs.forgeconfigapiport:forgeconfigapiport-fabric:${rootProject.forge_config_api_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "architectury.common.json"
    exclude "paraglider.accesswidener"
    exclude "paraglider.fabric.accesswidener"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    injectAccessWidener = true
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveBaseName = "Paraglider-fabric"
    archiveClassifier = null
}

jar {
    archiveClassifier = "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
